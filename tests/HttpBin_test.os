// BSLLS:MagicNumber-off
// BSLLS:UnusedLocalVariable-off

#Использовать asserts
#Использовать 1connector
#Использовать "../src/core"

Перем _HttpBin; // HttpBin
Перем _ТекущийКаталог; // Строка

&ТестовыйНабор(Характер = "Одиночка")
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

&ПередКаждым
Процедура ПередЗапускомТеста() Экспорт
	_ТекущийКаталог = ТекущийКаталог();
КонецПроцедуры

&ПослеКаждого
Процедура ПослеЗапускаТеста() Экспорт
	
	УстановитьТекущийКаталог(_ТекущийКаталог);

	Если Не _HttpBin = Неопределено Тогда
		_HttpBin.Остановить();
	КонецЕсли;

КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСервисСинхронноИОстановить() Экспорт

	// Действие
	_HttpBin = Новый HttpBin().Запустить();

	// Проверка
	Ожидаем.Что(_HttpBin.Отвечает(), "Должен быть запущен").ЭтоИстина();

КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСервисАсинхронноИОстановить() Экспорт

	// Подготовка
	_HttpBin = Новый HttpBin();	
	
	ВремяНачалаЗапуска = ТекущаяУниверсальнаяДатаВМиллисекундах();
	_HttpBin.Запустить();
	ВремяЗапуска = ТекущаяУниверсальнаяДатаВМиллисекундах() - ВремяНачалаЗапуска;
	_HttpBin.Остановить();

	Приостановить(200);
	
	// Действие
	_HttpBin.ЗапуститьАсинх();

	Приостановить(ВремяЗапуска);

	// Проверка
	Ожидаем.Что(_HttpBin.Отвечает(), "Должен быть запущен").ЭтоИстина();

КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСервисСКастомнымКонтроллеромИзФайла() Экспорт

	// Подготовка
	_HttpBin = Новый HttpBin()
		.УстановитьРасположениеКонтроллеров("./tests/fixtures/КастомныеКонтроллеры/КастомныйКонтроллер.os")
		.Запустить();

	// Действие
	Ответ = ВыполнитьЗапрос("/order/add").Текст();

	// Проверка
	Ожидаем.Что(Ответ).Равно("success");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСервисСКастомнымКонтроллеромИзКаталога() Экспорт

	// Подготовка
	_HttpBin = Новый HttpBin()
		.УстановитьРасположениеКонтроллеров("./tests/fixtures/КастомныеКонтроллеры")
		.Запустить();

	// Действие
	Ответ = ВыполнитьЗапрос("/order/add").Текст();

	// Проверка
	Ожидаем.Что(Ответ).Равно("success");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСервисСКастомнымКонтроллеромИзКаталогаСИзмененнымТекущийКаталогом() Экспорт

	// Подготовка
	УстановитьТекущийКаталог("tests");

	_HttpBin = Новый HttpBin()
		.УстановитьРасположениеКонтроллеров("./fixtures/КастомныеКонтроллеры")
		.Запустить();

	// Действие
	Ответ = ВыполнитьЗапрос("/order/add").Текст();

	// Проверка
	Ожидаем.Что(Ответ).Равно("success");

КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСервисИзмененнымиТекущимКаталогом() Экспорт

	// Подготовка
	УстановитьТекущийКаталог("tests");

	// Действие
	_HttpBin = Новый HttpBin().Запустить();

	// Проверка
	Ожидаем.Что(_HttpBin.Отвечает(), "Должен быть запущен").ЭтоИстина();

КонецПроцедуры

Функция ВыполнитьЗапрос(АдресРесурса)
	Возврат КоннекторHTTP.Get(_HttpBin.URL(АдресРесурса), , Новый Структура("Таймаут", 3));
КонецФункции