#Использовать logos

Перем _Лог; // Лог

#Область ПрограммныйИнтерфейс

// Возвращает приоритет провайдера
//
//  Возвращаемое значение:
//   Число - текущий приоритет провайдера
//
Функция Приоритет() Экспорт
	Возврат 1;
КонецФункции

// Возвращает идентификатор провайдера
//
//  Возвращаемое значение:
//   Строка - текущий идентификатор провайдера
//
Функция Идентификатор() Экспорт
	Возврат "cfg";
КонецФункции

// Возвращает тип провайдера
//
//  Возвращаемое значение:
//   Строка - текущий тип провайдера
//
Функция ТипПровайдера() Экспорт
	Возврат "file";
КонецФункции

// Возвращает расширения файлов по умолчанию
//
//  Возвращаемое значение:
//   Строка - расширения файлов по умолчанию
//
Функция РасширенияФайлов() Экспорт
	Возврат "cfg";
КонецФункции

// Выполняет чтение параметров для провайдера
//
// Параметры:
//   НастройкиПровайдера - Структура - структура настроек провайдера
//
//  Возвращаемое значение:
//   Соответствие - результат чтения провайдера
//
Функция ПрочитатьПараметры(НастройкиПровайдера) Экспорт

	ПрочитанныеПараметры = Новый Соответствие;

	ФайлПараметров = Новый Файл(НастройкиПровайдера.ФайлПараметров);

	Если Не ФайлПараметров.Существует() Тогда
		_Лог.Отладка("Не найден файл параметров <%1>", ФайлПараметров.ПолноеИмя);
		Возврат ПрочитанныеПараметры;
	КонецЕсли;

	_Лог.Отладка("Выполняю чтение файла параметров <%1>", ФайлПараметров.ПолноеИмя);
	ПрочитанныеПараметры = Прочитать(ФайлПараметров.ПолноеИмя);

	Возврат ПрочитанныеПараметры;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриСозданииОбъекта()
	_Лог = Логирование.ПолучитьЛог("oscript.lib.httpbin.configor.cfg");
КонецПроцедуры

// Выполнить чтение настроек из файла
//
// Параметры:
//   ПутьКФайлу - Cтрока - путь к файлу настроек
// Возвращаемое значение:
//   Соответствие - итоговые параметры
//
Функция Прочитать(Знач ПутьКФайлу)

	НастройкиИзФайла = Новый Соответствие;

	ТД = Новый ТекстовыйДокумент();
	ТД.Прочитать(ПутьКФайлу);

	Для Сч = 1 По ТД.КоличествоСтрок() Цикл

		СтрокаНастроек = ТД.ПолучитьСтроку(Сч);

		Если ПустаяСтрока(СтрокаНастроек) Или Лев(СтрокаНастроек, 1) = "#" Тогда
			Продолжить;
		КонецЕсли;

		Поз = СтрНайти(СтрокаНастроек, "=");
		Если Поз = 0 Тогда
			Продолжить;
		КонецЕсли;

		Ключ = СокрЛП(Лев(СтрокаНастроек, Поз - 1));
		Значение = СокрЛП(Сред(СтрокаНастроек, Поз + 1));

		НастройкиИзФайла.Вставить(Ключ, Значение);

	КонецЦикла;

	_Лог.Отладка("Прочитан файл настроек %1", ПутьКФайлу);

	Возврат НастройкиИзФайла;

КонецФункции // Прочитать

#КонецОбласти